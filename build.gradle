repositories {
    google()
    mavenCentral()
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            Set<File> fileSet = options.bootstrapClasspath.getFiles()
            List<File> newFileList = new ArrayList<>()
            newFileList.add(new File("./system_libs/framework.jar"))
            newFileList.add(new File("./system_libs/classes.jar"))
            newFileList.add(new File("./system_libs/classes2.jar"))
            newFileList.add(new File("./system_libs/classes3.jar"))
            newFileList.add(new File("./system_libs/classes4.jar"))
            newFileList.addAll(fileSet)
            options.bootstrapClasspath = files(
                    newFileList.toArray()
            )
        }
    }
}

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.3.1'
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.9.1"
    }
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

apply plugin: 'com.android.application'
apply plugin: "com.google.protobuf"

protobuf {
    protoc {
        // You still need protoc like in the non-Android case
        artifact = 'com.google.protobuf:protoc:3.0.0'
    }
    plugins {
        javalite {
            // The codegen for lite comes as a separate artifact
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks.generatedFilesBaseDir = 'gen-src'
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                // In most cases you don't need the full Java output
                // if you use the lite output.
                remove java
            }
            task.plugins {
                javalite { }
            }
        }
    }
}

android {
    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 33
        targetSdkVersion 33
    }

    sourceSets {
        main {
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            java.srcDirs = ['src', 'gen-src']
            proto.srcDirs = ['src']
            manifest.srcFile 'AndroidManifest.xml'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        debug {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    lint {
        ignore 'ProtectedPermissions', 'ExtraTranslation', 'ImpliedQuantity', 'MissingQuantity', 'MissingTranslation'
    }
}

dependencies {
    compileOnly files('./system_libs/framework.jar')
    compileOnly files('./system_libs/classes.jar')
    compileOnly files('./system_libs/classes2.jar')
    compileOnly files('./system_libs/classes3.jar')
    compileOnly files('./system_libs/classes4.jar')

    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.preference:preference:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'com.google.protobuf:protobuf-lite:3.0.0'
}
